plugins {
    id 'java-library'
    id 'nu.studer.jooq' version '3.0.2'
    id 'org.flywaydb.flyway' version '6.5.7'
}

group = 'com.rc'
version = '0.1.0-SNAPSHOT'

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

def mysqlVersion = '8.0.30'
def jooqVersion = '3.13.4'
def jooqGenDataSourceUrl = project.findProperty("jooqGenDataSourceUrl") ?:
    "jdbc:mysql://localhost:3306/rc_jooq?useUnicode=true&characterEncoding=UTF-8&serverTimezone=Asia/Shanghai"
def jooqGenDataSourceUrlUser = project.findProperty("jooqGenDataSourceUrlUser") ?: "root"
def jooqGenDataSourceUrlPassword = project.findProperty("jooqGenDataSourceUrlPassword") ?: "NewStrongPass!"
def jooqGenDataSourceDriver = project.findProperty("jooqGenDataSourceDriver") ?: "com.mysql.cj.jdbc.Driver"

dependencies {
    api project(':rc-core')
    implementation 'org.springframework:spring-context:5.3.33'
    implementation "org.jooq:jooq:${jooqVersion}"
    implementation 'javax.annotation:javax.annotation-api:1.3.2'

    runtimeOnly "mysql:mysql-connector-java:${mysqlVersion}"
    jooqRuntime "mysql:mysql-connector-java:${mysqlVersion}"
}

jooq {
    version = jooqVersion
    edition = 'OSS'
    main(sourceSets.main) {
        jdbc {
            driver = jooqGenDataSourceDriver
            url = jooqGenDataSourceUrl
            user = jooqGenDataSourceUrlUser
            password = jooqGenDataSourceUrlPassword
        }
        generator {
            name = 'org.jooq.codegen.JavaGenerator'
            database {
                name = 'org.jooq.meta.mysql.MySQLDatabase'
                inputSchema = 'rc_jooq'
                outputSchemaToDefault = true
                outputCatalogToDefault = true
                forcedTypes {
                    forcedType {
                        name = 'INTEGER'
                        expression = '.*'
                        types = 'TINYINT|TINYINT UNSIGNED|TINYINT\\(4\\)'
                    }
                }
            }
            generate {
                daos = false
                records = true
                pojos = true
                immutablePojos = false
                fluentSetters = true
                javaTimeTypes = true
            }
            target {
                packageName = 'com.rc.db'
                directory = "${project.projectDir}/src/main/java"
            }
        }
    }
}

tasks.named("compileJava") {
    dependsOn.remove("generateMainJooqSchemaSource")
}

tasks.named("generateMainJooqSchemaSource") {
    outputs.upToDateWhen { false }
    dependsOn("flywayMigrate")
}

flyway {
    url = project.findProperty("flywayUrl") ?:
        "jdbc:mysql://localhost:3306/rc_jooq?useUnicode=true&characterEncoding=UTF-8&serverTimezone=Asia/Shanghai"
    user = project.findProperty("flywayUser") ?: "root"
    password = project.findProperty("flywayPassword") ?: jooqGenDataSourceUrlPassword
    locations = ["filesystem:${rootProject.projectDir}/database"]
}
